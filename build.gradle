buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.27.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.4.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.11.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.26.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.11.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:4.173.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.15.0'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

version System.env.CIRCLE_TAG ?: gitVersion()

javaVersions {
    libraryTarget = 17
}

allprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'java-library'

    version rootProject.version
    group 'net.ckozak.immutables.repro'

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    // Turn off error-prone in order to reproduce failures from the command-line
    tasks.withType(JavaCompile, { it.options.errorprone.enabled = false})
}

dependencies {
    compileOnly 'org.immutables:value::annotations'
    annotationProcessor 'org.immutables:value'
}